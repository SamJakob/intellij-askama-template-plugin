plugins {
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.2.1'
}

group 'com.samjakob'
version '2025.1.1'

/** The version of the IDEA SDK to build against. */
def ideaSdkVersion = '2025.1'
/** The maximum build of IDEA that is supported (blank = any). */
def ideaBuildUntil = ''
/** The version of the Rust plugin that must be installed. */
def rustPluginVersion = '251.18673.38'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()

    intellijPlatform {
        defaultRepositories()
    }
}

sourceSets.main.java.srcDirs 'src/main/gen'

intellijPlatform {
    buildSearchableOptions = false

    pluginConfiguration {
        ideaVersion {
            untilBuild = ideaBuildUntil == null || ideaBuildUntil.isBlank()
                    ? provider { null }
                    : ideaBuildUntil
        }
    }
}

repositories {
    intellijPlatform {
        marketplace()
    }
}

dependencies {
    intellijPlatform {
        // I want to support IntelliJ Community Edition too, but the Rust plugin is no longer available for Community
        // Edition as of 2024.x. (See: https://plugins.jetbrains.com/plugin/8182--deprecated-rust/versions).
        // So, I'm going to support Ultimate Edition unless people need Community Edition support and I can figure out
        // how to make that happen.
        intellijIdeaUltimate ideaSdkVersion
        plugin("com.jetbrains.rust:${rustPluginVersion}")

        pluginVerifier()
        zipSigner()

        instrumentationTools()
    }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

def env = new Properties()
file(".env").withInputStream { env.load(it) }

publishPlugin {
    token = env.getProperty("JBM_TOKEN")
}
